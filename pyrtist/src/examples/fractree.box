(**expand:boxer-boot*)
(**boxer-version:0,2,0*)
include "g"
GUI = Void

(**boxer-refpoints:
gui1 1 34.569642781748975 2.5724167210506725 bbox1 1 -33.3736933815
87.8159051329 bbox2 1 76.1030045728 -1.65034200668
*)
(**end:expand*)(**expand:define-all*)
gui1 = Point[.x=34.5696427817, .y=2.57241672105]
bbox1 = Point[.x=-33.3736933815, .y=87.8159051329]
bbox2 = Point[.x=76.1030045728, .y=-1.65034200668]

(**end:expand*)

include "tools"

// We define a new object, which contains all the details about
// the Pythagorean tree
PythagTree = (Int level, Point p1, p2, Real xmid, h)

PythagTree@Figure[
  xmid = $.xmid, h = $.h, level = $.level, p1 = $.p1, p2 = $.p2, d = 1e-10
  Color[g = 1.0/(level+1), b = 1 - g, (0.5*b, g, 0)]

  side = Points[p1, p2]
  p = Poly[side.Get[0], side.Get[1-d], side.Get[(1-d, 1)], side.Get[(0, 1)]]
  t = Points[p.Get[2], p.Get[3], p.Get[(2+xmid, -h)]]

  p // Pass the Polygon to the window for drawing

  If[level >= 1]
    PythagTree[.level = level-1, .xmid = xmid, .h = h
               .p1 = t.Get[2], .p2 = t.Get[0]]
    PythagTree[.level = level-1, .xmid = xmid, .h = h
               .p1 = t.Get[1], .p2 = t.Get[2]]
]

w = Figure[
  BBox[bbox1, bbox2]

  // Now we can draw the tree with the following line
  gui_r = GUIReal[Interval[0, 15], gui1, "level="]
  gui_r.value = l = Int[gui_r.value]

  PythagTree[.level=l, .xmid=0.6, .h=0.45, .p1=(5, 10), .p2=(25, 10)]

  //.Save["fractree.png"]

  gui_r // plot the bar
]

(**expand:view:w*)(**end:expand*)
