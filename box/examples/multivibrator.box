include "g"
include "electric"

// These parameters control the spacing of the circuit
sx = 15.0, sy = 30.0 // scales along x and y directions
// x offsets and y offsets
x0 = 0.0, x1 = sx, x2 = 2.0*sx, x3 = 3.5*sx, x4 = 4.5*sx
y0 = 0.0, y1 = sy, y2 = 2.0*sy

w = Window[][
  // Place the four resistors, the two condenser and the two NPN transistors
  r1_ = .Put[resistor, "rt", .Near[1, (x1, y0)], .Near[2, (x1, y1)]]
  r2_ = .Put[resistor, "rt", .Near[1, (x2, y0)], .Near[2, (x2, y1)]]
  r3_ = .Put[resistor, "rt", .Near[1, (x3, y0)], .Near[2, (x3, y1)]]
  r4_ = .Put[resistor, "rt", .Near[1, (x4, y0)], .Near[2, (x4, y1)]]
  c1_ = .Put[capacitor, "rt", .Near[1, (x1, y1)], .Near[2, (x2, y1)]]
  c2_ = .Put[capacitor, "rt", .Near[1, (x4, y1)], .Near[2, (x3, y1)]]
  t1_ = .Put[transistor_NPN, "rt", .Near["C", (x1, y1)], .Near["E", (x1, y2)]]
  t2_ = .Put[transistor_NPN, "rt", .Scale[(1, -1)],
             .Near["C", (x4, y1)], .Near["E", (x4, y2)]]

  // These lines should disappear in future versions of the Box language
  r1 = PointList[r1_], r2 = PointList[r2_], r3 = PointList[r3_]
  r4 = PointList[r4_], c1 = PointList[c1_], c2 = PointList[c2_]
  t1 = PointList[t1_], t2 = PointList[t2_]

  // Trace all the connections between the components
  \ .Line[0.2, (x0, y0), (x4, y0), r4.Get[1];
          (x1, y0), r1.Get[1]; (x2, y0), r2.Get[1];
          (x3, y0), r3.Get[1];
          r1.Get[2], t1.Get["C"]; r4.Get[2], t2.Get["C"];
          t1.Get["E"], (x1, y2); t2.Get["E"], (x4, y2), (x0, y2);
          r2.Get[2], (x2, y1), c1.Get[2]; c1.Get[1], (x1, y1);
          r3.Get[2], (x3, y1), c2.Get[2]; c2.Get[1], (x4, y1);
          (x2, y1), Point[t2.Get["B"]] - (sx*0.2, 0), t2.Get["B"];
          (x3, y1), Point[t1.Get["B"]] + (sx*0.2, 0), t1.Get["B"]
  ]

  // Insert small circles, where more than 3 lines meet
  \ .Circle[0.6, (x1, y0); (x2, y0); (x3, y0); (x1, y2);
            (x2, y1); (x3, y1); (x1, y1); (x4, y1)]

  // Some labels near the components
  on_left = Point[(-8.5, -1.5)], on_right = Point[(4.0, -1.5)]
  on_top = Point[(-3.0, 5.0)]
  \ .Text[.Font["Arial", 4.0],
          "R1", Point[r1.Get[1.5]] + on_left;
          "R2", Point[r2.Get[1.5]] + on_right;
          "R3", Point[r3.Get[1.5]] + on_left;
          "R4", Point[r4.Get[1.5]] + on_right;
          "T1", Point[t1.Get[1.5]] + on_left;
          "T2", Point[t2.Get[1.5]] + on_right;
          "C1", Point[c1.Get[1.5]] + on_top;
          "C2", Point[c2.Get[1.5]] + on_top]

  \ .Show[Point[t2.Get[1.5]] + 3*on_right] // Enlarge bounding box
]

// Save the figure into a postscript file
w.Save["multivibrator.eps"]

