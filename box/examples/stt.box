include "g"
include "arrows"

t = Real
t = 0.05

lmc = Color[color.red,    .a=0.5] // local moment color
sc  = Color[color.blue,   .a=0.9] // system color
cc  = Color[color.yellow, .a=0.8] // conduction electron color

lmr = 4.0   // local moment radius
lmal = 10.0 // local moment arrow length
lmalw = 0.5 // local moment arrow line width
sw = 100.0  // system half width
sh = 20.0   // system half height
dww = 50.0  // domain wall width
n = 10      // number of localized moments

w = Window[]
sw2 = sw + lmal
\ w.Poly[sc, (-sw2, -sh), (sw2, -sh), (sw2, sh), (-sw2, sh)]
\ w.Line[color.black, 0.5, (-sw2, -sh), (sw2, -sh);
         (-sw2, sh), (sw2, sh)]

i = 0
[
  x = (2.0*i/(n - 1) - 1.0)*sw
  u = 0.5*(1 - Min[1, Max[-1, x/dww]]) // 1...0 for u:-1...1
  angle = -const.pi*u
  v = lmal*Vec[angle]
  p = (x, 0)
  \ w.Circle[p, lmr, lmc]
  \ w.Line[color.black, p-v, lmalw, arrow_triangle, p+v]

  For[++i < n]
]


tn = Int[t*n]/Real[n]
x = sw*(2*t - 1)
angle = -const.pi*(1 - tn)
v = lmal*Vec[angle]
cp = (x, sh*0.5)
\ w.Circle[cp, lmr, cc]
\ w.Line[color.black, cp-v, lmalw, arrow_triangle, cp+v]

w.Save["stt.png"]

