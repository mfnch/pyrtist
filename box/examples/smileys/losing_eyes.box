// Programmino per disegnare uno smiley che perde letteralmente gli occhi
//  t e' il tempo trascorso dall'inizio
//  a e' il parametro di attrito nell'urto
//  g e' l'accelerazione gravitazionale
//  v0 e' la velocita' all'urto numero 0

include "g"

rate = 5.0*t // velocita' di cambiamento (bocca e capelli)

a = 0.8
g = 10.0
v0 = 9.5

Vers = Point
(Int->Real)@Vers[angle=$*0.01745329251994329576, $$=(Cos[angle], Sin[angle])]

//-------------------------------------------------------------------------------
// Calcoli per trovare l'altezza degli occhi
t0 = 2.0*v0/g
t = t + t0/2.0
n  = Int[Log[(a - 1.0)*t/t0 + 1.0] / Log[a]]
aN = Exp[n*Log[a]]
vN = aN * v0
tN = t0 * (aN - 1.0) / (a - 1.0)
delta_t = t - tN
h = -delta_t * (vN - 0.5*g*delta_t) + v0*v0/(2.0*g)

sx = 5.0 - 0.0*(1.0-Exp[-rate])
sy = 0.5 + 1.0*(1.0-Exp[-rate])
q = (1.0-Exp[-rate])*3.0
w = (1.0-Exp[-rate])*0.05

//-------------------------------------------------------------------------------
// Descrizione fisica dello smiley:
r = 10                   // Raggio della faccia
s = 0.25                 // Spessore delle linee di contorno
ro = 2.5                 // Raggio degli occhi
rp = 0.4*ro              // Raggio della pupilla
od = Point[(3.5, -2.0)]  // Posizione dell'occhio destro
gr = 1                   // Raggio guancie
gd = Point[(6.0, 2.0)]   // Posizione della guancia destra

//-------------------------------------------------------------------------------
s2 = s+s

os = Point[(-od.x, od.y)] // Posizione dell'occhio sinistro
gs = Point[(-gd.x, gd.y)] // Posizione della guancia sinistra
// pd e ps sono le posizioni della pupilla destra e sinistra rispett.

pd = od + (0, h*ro)
ps = os + (0, h*ro)

cf = Color[.r=1.0, .g=1.0, .b=0.0] // Colore faccia

smile = Window["fig"]

\ smile[
  // Disegno la faccia
  \ .Circle[color.black, (0, 0), r+s; cf, r-s]

  // Bocca
  b = 4.5 // Posizione in y della bocca
  \ .Circle[color.black, sx, sy, (0, b);
            color.red, sx-s2, sy-s2]
  \ .Circle[color.black, os, ro; od;
            color.white, os, ro-s2; od]
  \ .Circle[color.black, rp, rp, pd; ps] // Pupille

  // Capelli
  spacing=10.0 // spaziatura in gradi fra i capelli
  angle=-90.0
  a = angle
  \ .Line[color.black, s, Vers[a]*r, Vers[a]*(r+q)]
  a = a - spacing
  \ .Line[color.black, s, Vers[a]*r, Vers[a]*(r+q)]
  a = a - spacing
  \ .Line[color.black, s, Vers[a]*r, Vers[a]*(r+q)]
  a = a - spacing
  \ .Line[color.black, s, Vers[a]*r, Vers[a]*(r+q)]
  a = angle + spacing
  \ .Line[color.black, s, Vers[a]*r, Vers[a]*(r+q)]
  a = a + spacing
  \ .Line[color.black, s, Vers[a]*r, Vers[a]*(r+q)]
  a = a + spacing
  \ .Line[color.black, s, Vers[a]*r, Vers[a]*(r+q)]
]

main = Window["bm4", (220, 250)]
main.Put[smile, (110, 140), .Scale[10]]
main.Save["smiley.bmp"]
