// Run with "box wheatstone.box -l g"
include "g"
include "electric"

wheatstone = Window["fig"][
  d1 = 20.0 // d1 = half diagonal
  p1 = (0, -d1)
  p2 = (d1, 0)
  p3 = (0, d1)
  p4 = (-d1, 0)
  \ .Hot[p1, p2, p3, p4]

  // Places the four resistors
  st_r12 = .Put[resistor, "rt", .Near[1, p1], .Near[2, p2]]
  st_r23 = .Put[diode, "rt", .Near[1, p2], .Near[2, p3]]
  st_r34 = .Put[inductance, "rt", .Near[1, p3], .Near[2, p4]]
  st_r41 = .Put[resistor, "rt", .Near[1, p4], .Near[2, p1]]
  r12 = PointList[st_r12], r23 = PointList[st_r23]
  r34 = PointList[st_r34], r41 = PointList[st_r41]

  // Connecting the four resistors
  \ .Line[0.2, p1, r12.Get[1]; r12.Get[2], p2; r12.Get[2], p2; p2, r23.Get[1];
          r23.Get[2], p3; p3, r34.Get[1]; r34.Get[2], p4; p4, r41.Get[1];
          r41.Get[2], p1]

  // Drawing small circles on the nodes
  \ .Circle[0.8, p1; p2; p3; p4]
]

//-------------------------------------------------------------------------------
Print["Opening a new 'bm4' windows to save the figure.";]
main = Window["bm4", (200, 200)][\ .Put[wheatstone, (100, 100), .Scale[4]]]
main.Save["wheatstone.bmp"]

Print["Opening a new 'ps' windows to save the figure.";]
\ Window["ps", .File["wheatstone.ps"]][
  \ .Put[wheatstone, (100, 100), .Scale[3]]
]

