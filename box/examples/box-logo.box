// Under a shell type: box box-logo.box -l g
// ---
// This Box source was created to draw the Box logo.
// It is obtained by just four variable-width lines and one ring.

include "g"

lsc = LineStyle[7.0]
lsb = LineStyle[5.0]
lsa = LineStyle[3.0]
w = Window[][
  // The X is made by just two lines with variable widths
  \ .Line[0.5, lsb,
          (31, 45), 0.7, 0.4, (28, 36), 0.4, 0.5, (24, 31), 0.6, (24, 30);
          (25, 42), 0.7, 0.6, (28, 36), 0.5, 0.4, (33.5, 32),
          (34, 27), 0.4, 0.5, (32, 22)]
  // The B is the more complicated thing to trace...
  \ .Line[lsb
          0.4, (24.5, 34.5), 0.45, (16, 26), 0.5, 0.55, (12.5, 25.5);
          0.1, (12.6, 25), 0.2, 0.25, (12.5, 22.5), 0.3, 0.35, (16, 21.5), 0.3
          0.25, (18, 24), 0.2,  lsa, (18.5, 26), (18, 28), (17, 29), (16, 28)
          (16.5, 26), (18.5, 24.5),  lsb, 0.3, 0.5, (24, 24), lsc, 0.4
          0.35, (23, 30), 0.3, 0.2, (19, 35)]

  // The o is a ring
  \ .Circle[(c=(28.8, 30.8)), 2.3; c+(0.1, 0.1), 1.5]
]

\ Window[][
  \ .Put[w, .Scale[(2, 2)]]
  .Save["box-logo.png"]
]

