include "g"
include "arrows"

w = Window[]

// Draw one plain polygon
//{sharp
a = (5, 40), b = (50, 50),  c = (55, 0),  d = (15, 10)
pl = w.Poly[color.blue, a, b, c, d]
//}
w.Save["html/poly1.png"]

// Draw another polygon from the mid-points of the edges of the original one
//{midpoints
i = 1, \ w.Poly[color.yellow, pl.Get[i+0.5], For[++i < 5]]
\ w.Circle[Color[color.red, .a=0.5], 1, pl] // Circles on vertices
//}
w.Save["html/poly2.png"]

//{margins
\ w.Poly[color.yellow
         a, 0, 0.05, b, 0.1, 0.15, c, 0.2, 0.5, d, 0.2, 0]
//}
w.Save["html/poly3.png"]

//{one_margin
\ w[Style[.Fill[";"]]]
\ w.Poly[color.magenta, 0.2, pl;
         Color[color.red, .a=0.7], 0.5, pl]
//}
w.Save["html/poly4.png"]

//{big_margin
\ w.Poly[Color[color.green, .a=0.7], 0, 1, pl]
//}
w.Save["html/poly5.png"]

w2 = Window[]
//{ellipse
rect = w2.Poly[color.red, (0, 0), (50, 0), (50, 30), (0, 30)] // a rectangle
\ w2.Poly[color.yellow, 0.25, rect] // partial rounding
\ w2.Poly[color.magenta, 0.5, rect] // full rounding makes it an ellipse!
//}
w2.Save["html/ellipse.png"]

//{etc
// Rulers
\ w.Line[Color[color.black, .a=0.5], 0.4, h=0.03
         arrow_ruler, pl.Get[(1, -h)], arrow_ruler, pl.Get[(1.999, -h)];
         arrow_ruler, pl.Get[(1, h)], arrow_ruler, pl.Get[(1.2, h)];
         arrow_ruler, pl.Get[(1.8, h)], arrow_ruler, pl.Get[(1.999, h)]]

// Labels
w.Text[.Font["Sans", 5], pl.Get[1.5], .From[(1, -0.5)], "d";
       .Font["Sans", 2], pl.Get[1.1], .From[(1, -1.2)], "0.2*d"]
//}

w.Save["html/poly6.png"]
