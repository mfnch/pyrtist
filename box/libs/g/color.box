///Section: g
///Intro: Implementation of Color. Automatically included when including "g".
// Definition of the type Color and some example of Color-s

include "obj"
include "pattern"

///Intro: Color allows to define new colors.
///Desc: A color object is made of four real numbers, one for each component
//  of the color: red, green, blue and the alpha channel. These components are
//  initialized by default to zero, unless specified otherwise. See the
//  examples for possible ways of creating a Color.
///Where: red, green, blue, alpha are Real numbers
///Example: red = Color[.r=1.0]
///Example:
// grey = Color[.r=.g=.b=0.5]    // grey color (red = green = blue = 0.5)
// grey = Color[(0.5, 0.5, 0.5)] // does the same
// grey = Color[0.5]             // does the same, too
Color = ++(Real r, g, b, a)

([)@Color[$$.r = $$.g = $$.b = 0.0, $$.a = 1.0]

///Intro: set from another color.
Color@Color[$$.r = $.r, $$.g = $.g, $$.b = $.b, $$.a = $.a]

///Intro: set to the given level of grey (0 for black, 1 for white).
Real@Color[$$.r = $$.g = $$.b = $]

(Real r, g, b)@Color[$$.r = $.r, $$.g = $.g, $$.b = $.b]
(Real r, g, b, a)@Color[$$.r = $.r, $$.g = $.g, $$.b = $.b, $$.a = $.a]

Color@Str["Color[.r=", $.r, ", .g=", $.g, ", .b=", $.b, ", .a=", $.a, "]"]
Color@Print[Str[$]]

///Intro: create the pattern from the color.
Color@Pattern[
  $$.cmdstream.Empty[]
  \ $$.cmdstream[Obj[const.raw.pattern_create_rgba, $.r, $.g, $.b, $.a]]
]

///Intro: a color given in terms of Hue, Saturation and Value.
HSV = ++(Real h, s, v, a)
Hsv = HSV

(.[)@HSV[.h = 0.0, .s = .v = .a = 1.0]

///Intro: set the HSV object from the given HSV object.
HSV@HSV[.h = $.h, .s = $.s, .v = $.v, .a = $.a]

Real@HSV[$$.h = $, $$.s = $$.v = $$.a = 1.0]

///Intro: set the HSV object from a triple ``(hue, saturation, value)``.
(Real h, s, v)@HSV[$$.h = $.h, $$.s = $.s, $$.v = $.v]

///Intro: set the HSV object from a quadruple
// ``(hue, saturation, value, alpha)``.
(Real h, s, v, a)@HSV[$$.h = $.h, $$.s = $.s, $$.v = $.v, $$.a = $.a]

///Intro: print the HSV object.
HSV@Print["HSV[.h=", $.h, ", .s=", $.s, ", .v=", $.v, ", .a=", $.a, "]"]

///Intro: get the color corresponding to the given HSV object.
Color@HSV "hsv_color" ?

///Intro: get the HSV value for the given color.
HSV@Color "color_hsv" ?

// To make a color darker
Color.Darker = Void
Real@Color.Darker[
  c = Color[HSV[$$$, .v *= Max[0, $]]]
  $$$.r = c.r, $$$.g = c.g, $$$.b = c.b
]

///Intro: convert a Color to an Obj.
Color@Obj[Obj[Point[.x=$.r, .y=$.g], Point[.x=$.b, .y=$.a]]]

///Intro: make a Color out of an Obj.
Obj@Color[
  rg_comp = Point[$.Get[0]]
  ba_comp = Point[$.Get[1]]
  .r = rg_comp.x, .g = rg_comp.y
  .b = ba_comp.x, .a = ba_comp.y
]

color = (Color black, red, green, yellow, blue, magenta, cyan, white
               dark_red, dark_green, dark_yellow, dark_blue, dark_magenta
               dark_cyan, grey, none)[
  on  = 1.0 // Maximum intensity
  mid = 0.5 // Medium intensity
  off = 0.0 // Minimum intensity

  .black.r    = off, .black.g    = off, .black.b    = off, .black.a    = on
  .red.r      =  on, .red.g      = off, .red.b      = off, .red.a      = on
  .green.r    = off, .green.g    =  on, .green.b    = off, .green.a    = on
  .yellow.r   =  on, .yellow.g   =  on, .yellow.b   = off, .yellow.a   = on
  .blue.r     = off, .blue.g     = off, .blue.b     =  on, .blue.a     = on
  .magenta.r  =  on, .magenta.g  = off, .magenta.b  =  on, .magenta.a  = on
  .cyan.r     = off, .cyan.g     =  on, .cyan.b     =  on, .cyan.a     = on
  .white.r    =  on, .white.g    =  on, .white.b    =  on, .white.a    = on

  .dark_red.r    = mid, .dark_red.g    = off, .dark_red.b    = off, .dark_red.a    = on
  .dark_green.r  = off, .dark_green.g  = mid, .dark_green.b  = off, .dark_green.a  = on
  .dark_yellow.r = mid, .dark_yellow.g = mid, .dark_yellow.b = off, .dark_yellow.a = on
  .dark_blue.r   = off, .dark_blue.g   = off, .dark_blue.b   = mid, .dark_blue.a   = on
  .dark_magenta.r= mid, .dark_magenta.g= off, .dark_magenta.b= mid, .dark_magenta.a= on
  .dark_cyan.r   = off, .dark_cyan.g   = mid, .dark_cyan.b   = mid, .dark_cyan.a   = on
  .grey.r        = mid, .grey.g        = mid, .grey.b        = mid, .grey.a        = on

  // Just to unset the local default color for the subobjects of Window
  .none.r = -1.0
  .none.g = .none.b = .none.a = 0.0
]
