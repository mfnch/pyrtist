#provide "g.curve"
///Section: g

include "raw"
include "style"
include "path"

///Intro: Extension of {type(Point)} which allow to specify an entering and
// exiting direction.
// The typical usage is ``Tri[pin, p, pout]``, ``Tri[p]`` (which is equivalent
// to ``Tri[p, p, p]``), ``Tri[pin, p]`` (equivalent to
// ``Tri[pin, p, 2*p - pin]``), ``Tri[; p, pout]`` (which is equivalent to
// ``Tri[2*p - pout, p, pout]``).
Tri = ^(Int have, num, Point p, pin, pout)

(.[)@Tri[.have = 0, .num = 0]

///Intro: Used to indicate that the first point is missing.
// Not to be used when points have been already given.
(;)@Tri[If[$$.num == 0], $$.num += 1, Else, Fail["Unexpected usage of ;"]]

///Intro: The entering, central or exiting point.
Point@Tri[
  If[$$.num > 2]
    Fail["Tri does take up to three points"]
  Else
    num = $$.num++
    If[num == 0]
      .pin = .p = .pout = $, .have = 2
    Else, If[num == 1]
      [If[$$.have & 2], .pin = .p, .p = $, .pout = 2*.p - .pin, .have = 3
       Else, .p = .pin = $   ]
    Else // num == 2
      .pout = $
      .have |= 4
      [If[!($$.have & 1)], .pin = 2*.p - .pout]
]

///Intro: get a string representation of the {type(.@)} object.
Tri@Str[
  num = $.num
  sep = ""
  "Tri["
  [If[num > 1], sep, $.pin, sep = ", "]
  [If[num > 0], sep, $.p, sep = ", "]
  [If[num > 2], sep, $.pout]
  "]"
]

///Intro: print the object.
Tri@Print[Str[$]]

///Intro: copy the {type(Tri)} object from the given one.
Tri@Tri[$$ = $]

///Intro: curved polygon.
Curve = ^(Array tris, Int close, Style style)

(.[)@Curve[.close = 0]

///Intro: whether to close the curved polygon.
Close@Curve[.close = 1]

///Intro: provide the next point (optionally, with directions).
(Point=>Tri)@Curve[\ .tris[$]]

///Intro: pattern to use for filling the polygon.
Pattern@Curve[\ .style[$]]

///Intro: style to use when drawing the polygon.
Style@Curve[\ .style[$]]

///Intro: append the curve path.
Curve@Path[
  num = Num[$.tris]
  If[num >= 2]
    first_tri = prev_tri = Tri[Get[$.tris, 0]]
    Obj[const.raw.move_to, prev_tri.p]

    i = 1
    [If[i < num]
       tri = Tri[Get[$.tris, i]]
       Obj[const.raw.curve_to, prev_tri.pout, tri.pin, tri.p]
       prev_tri = tri
     i += 1, For[1]]
   
     [If[$.close]
       Obj[const.raw.curve_to, prev_tri.pout, first_tri.pin, first_tri.p]]
     $$.empty = 0
]

///Intro: pass the polygon to the window for drawing.
Curve@Window[
  path = Path[close = $.close, $.close = 1, $, $.close = close]
  If[!path.empty]
    path.cmdstream
    CmdStream[$.style]
]
