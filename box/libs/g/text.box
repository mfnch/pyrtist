#provide "g.text"
///Section: g

include "raw"
include "style"
include "path"

///Intro: specifies a reference point used when translating objects (such as
// {type(Text)}, for example). In particular, if you specify that your figure
// should be translated to a point ``p``, then using ``From[(0.5, 0.5)]`` will
// make sure that the center of the bounding box of the figure will end up in
// ``p``. Using ``From[(0, 0)]`` will make sure that the bottom-left corner
// ends up to be in ``p``.
From = ++(Point point,)

(.[)@From[.point.x = .point.y = 0.5]

///Intro: set the reference point.
Point@From[.point = $]

///Intro: From[r] is equivalent to From[(r, r)]
Real@From[.point.x = .point.y = $]

///Intro: create a new text shape.
Text = ++((Int pos,) have
          Point pos
          From from
          Str text, Font font, Style style)

(.[)@Text[.have = (0,)]

///Intro: provide the text string.
Str@Text[\ .text[$]]

///Intro: the position point where to place the text.
Point@Text[.pos = $, .have.pos = 1]

///Intro: how to position the text with respect to the give position point.
From@Text[.from = $]

///Intro: set the style used to draw the text.
Style@Text[\ .style[$]]

///Intro: the color to use.
Color@Text[\ .style[$]]

///Intro: the gradient to use for the text.
Gradient@Text[\ .style[$]]

///Intro: the font to use.
Font@Text[.font = $, If[$.have.color], $.color]

///Intro: convert a Text to Obj
Text@Obj[
  If[$.have.pos]
    Obj[$.font, $.pos, $.from.point, $.text]
  Else[]
    Fail["Cannot convert Text to Obj: the position is missing"]
]

///Intro: convert an Obj to a Text
Obj@Text[
  [If[Num[$] != 4], Fail["Cannot convert Obj to Text"]]
  .have.pos = 1
  .font = Font[$.Get[0]]
  .pos = Point[$.Get[1]]
  .from.point = Point[$.Get[2]]
  .text = Str[$.Get[3]]
]

///Intro: add the text to the path.
Text@Path[
  If[$.have.pos && Length[$.text] > 0 && $.font.size > 0.0]
    p = $.pos
    fsz = $.font.size
    d = Point[.x=fsz, .y=0.0]
    d_ort = Point[.x=-d.y, .y=d.x]
    p_right = p + d
    p_up = p + d_ort
    Obj[const.raw.ext_set_font, $.font.name]
    Obj[const.raw.ext_text_path, p, p_right, p_up, $.from.point, $.text]
    $$.empty = 0
]

///Intro: pass the text to the window for drawing.
Text@Window[
  path = Path[$]
  If[!path.empty]
    path.cmdstream
    CmdStream[$.style]
]

