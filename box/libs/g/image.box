///Section: g

#provide "g.image"

///Intro: object representing an image taken from an external file.
Image = ++(Str filename, Axes axes, From from)

(.[)@Image[\ $$.from[(0.0, 0.0)]]

///Intro: filename containing the image.
Str@Image[.filename = $]

///Intro: provide the reference system for the image.
Axes@Image[.axes = $]

///Intro: point where the image should be placed.
// If ``p`` is a {type(Point)}, then ``Image[p, ...]`` is equivalent to
// ``Image[Axes[p], ...]``
Point@Image[\ .axes[$]]

///Intro: provide the point of the image to use when positioning the image.
From@Image[.from = $]

///Intro: use the picture as a pattern to fill and draw shapes.
Image@Pattern[
  \ $$.cmdstream[Obj[const.raw.pattern_create_image,
                     $.axes.origin, $.axes.one_zero, $.axes.zero_one
                     $.from.point, $.filename]]
]

///Intro: draw the picture on the window.
Image@Window[
  origin = $.axes.origin, xone = $.axes.one_zero, yone = $.axes.zero_one
  xyone = xone + yone - origin
  Poly[origin, xone, xyone, yone, Pattern[$]]
]
