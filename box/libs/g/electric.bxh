// Some symbols used in electronics
include "arrows"

//-------------------------------------------------------------------------------
diode = Window["fig"][
  a = 2.0, b = 3.0, d1 = 4.0, s = 0.4
  p1 = (-d1, 0), p2 = (b+s/2+d1, 0)
  p = Point
  \ .Hot[p1, p2]
  \ .Poly[(0, -a), (0, a), (b, 0)]
  \ .Poly[(p=(b-s/2, -a)), (p+=(s, 0)), (p+=(0, 2*a)), (p-=(s, 0))]
  \ .Line[0.2, p1, 0.2, p2]
]

//-------------------------------------------------------------------------------
resistor = Window["fig"][
  d1 = 1.0, v1 = Point[.x=1, .y=4], v2 = Point[.x=1, .y=-4]
  p0 = p = Point[.x=.y=0.0]
  \ .Line[line.sharp, 0.2, p, 0.2, (p+=(d1, 0)), (p+=v1/2)
    (p+=v2), (p+=v1), (p+=v2), (p+=v1), (p+=v2), (p+=v1)
    (p+=v2), (p+=v1), (p+=v2), (p+=v1), (p+=v2), (p+=v1/2), (p+=(d1, 0))
  ]
  \ .Hot[p0, p]
]

//-------------------------------------------------------------------------------
inductance = Window["fig"][
  a = 1.1, b = 2.2, d1 = 2.0, v = 0.5
  v1 = (a, b) + (v, 0)
  v2 = (a, -b) + (v, 0)
  v3 = (-a, -b) + (v, 0)
  v4 = (-a, b) + (v, 0)
  p0 = p = Point[.x=.y=0.0]
  \ .Line[
    line.smooth, 0.2, p, 0.2, (p+=(d1, 0))
    (p+=v1), (p+=v2), (p+=v3), (p+=v4), (p+=v1), (p+=v2), (p+=v3), (p+=v4)
    (p+=v1), (p+=v2), (p+=v3), (p+=v4), (p+=v1), (p+=v2), (p+=v3), (p+=v4)
    (p+=v1), (p+=v2), (p+=v3), (p+=v4), (p+=v1), (p+=v2), (p+=v3), (p+=v4)
    (p+=v1), (p+=v2), (p+=(d1, 0))
  ]
  \ .Hot[p0, p]
]

//-------------------------------------------------------------------------------
capacitor = Window["fig"][
  h = 4.0, d = 1.0
  \ .Line[line.sharp, 0.6, (-h, -d), (h, -d); (-h, d), (h, d)]
  \ .Hot[(0.0, -d), (0.0, d)]
]

//-------------------------------------------------------------------------------
transistor_NPN = Window[]
transistor_PNP = Window[]

[
  r = 5.0
  d = 0.4, hd = 0.5*d
  ld = r*0.4, ll = r*0.5, lp = r*0.2
  a = -0.65*const.pi
  base = (r-hd, 0)
  collector = (r-hd)*(Cos[a], Sin[a])
  emitter = (r-hd)*(Cos[-a], Sin[-a])
  \ transistor_NPN[
    \ .Circle[(0, 0), r; r-d]
    \ .Line[color.black, d, (ld, -ll), (ld, ll); hd, base, (ld, 0)]
    \ .Hot["C", collector, "E", emitter, "B", base]
  ]
  \ transistor_PNP[
    \ .Put[transistor_NPN]
    \ .Line[hd, collector, arrow_triangle, (ld-d, -lp);
            (ld, lp), emitter]
    \ .Hot["C", collector, "E", emitter, "B", base]
  ]
  \ transistor_NPN[
    \ .Line[(ld, -lp), collector; (ld, lp), arrow_triangle, emitter]
  ]
]
