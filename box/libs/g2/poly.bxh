#provide "g.poly"

include "raw"
include "style"
include "path"

///Intro: create a polygon.
Poly = ++(Obj args, Sty style, Int close)

(.[)@Poly[.close = 0]

///Intro: next vertex of the polygon.
Point@Poly[\ .args[$]]

///Intro: real number between 0 and 1 used to round the corners of the
// polygon. 0.0 gives sharp corners, 0.5 gives smooth corners, 1.0 gives
// alternating smooth and sharp corners.
Real@Poly[\ .args[$]]

///Intro: color to use for filling the polygon.
Color@Poly[\ .style[$]]

Sty@Poly[\ .style[$]]

///Intro: add the polygon to the path.
Poly@Path[
  n = Num[$.args]

  i = 0
  first_point = 1

  [If[i < n]
    arg_type = $.args.GetType[i]
    [If[arg_type = objtype.point]
       pt = Point[$.args.Get[i]]
       [If[first_point], Obj[g.raw.move_to, pt], first_point=0
        Else[], Obj[g.raw.line_to, pt]]
     Else[]]
  \ ++i, For[1]]

  // Close the polygon, if requested
  [If[$.close], Obj[g.raw.close_path]]

  .empty = first_point
]

Poly@Window[
  path = Path[$]
  If[!path.empty]
    path.cmdstream
    CmdStream[$.style]
]

///Intro: close a polygon.
Close = ++Void

Close@Poly[.close = 1]
