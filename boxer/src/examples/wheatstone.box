(**expand:boxer-boot*)
(**boxer-version:0,2,0*)
include "g"
GUI = Void

(**boxer-refpoints:
gui1 1 -0.255807878788 -19.5900623377 gui2 1 19.7791411043 -0.0303101075269
gui3 1 0.127607361963 19.7838873118 gui4 1 -20.5447852761 0.223718064516 gui5 1
-22.6152212121 21.856384472 gui6 1 22.357695082 -21.4080237288
*)
(**end:expand*)(**expand:define-all*)
gui1 = Point[.x=-0.255807878788, .y=-19.5900623377]
gui2 = Point[.x=19.7791411043, .y=-0.0303101075269]
gui3 = Point[.x=0.127607361963, .y=19.7838873118]
gui4 = Point[.x=-20.5447852761, .y=0.223718064516]
gui5 = Point[.x=-22.6152212121, .y=21.856384472]
gui6 = Point[.x=22.357695082, .y=-21.4080237288]

(**end:expand*)

include "electric"

// This is not a real Wheatstone bridge.
// We replace some of the resistors with other components, just to make
// the thing more interesting!
wheatstone = Figure[
  d1 = 20.0 // d1 = half diagonal
  BBox[gui5, gui6]
  p1 = Point[gui1], p2 = Point[gui2]
  p3 = Point[gui3], p4 = Point[gui4]

  // Places the four components.
  // Each component has two connections. Here we want to rotate and translate
  // ("rt") a resistor such that the first of its terminals is near to
  // the point p1 and the second one is near to p2.
  r12 = Put[resistor, "rt", Near[0, p1], Near[1, p2]]
  // We proceed in a similar way for the other components:
  r23 = Put[diode, "rt", Near[0, p2], Near[1, p3]]
  r34 = Put[inductance, "rt", Near[0, p3], Near[1, p4]]
  r41 = Put[resistor, "rt", Near[0, p4], Near[1, p1]]

  // Actually show the components
  r12, r23, r34, r41

  // Connecting the four resistors
  Lines[0.4, p1, r12.Get[0]; r12.Get[1], p2; p2, r23.Get[0]; r23.Get[1], p3;
        p3, r34.Get[0]; r34.Get[1], p4; p4, r41.Get[0]; r41.Get[1], p1;]

  // Drawing small circles on the nodes
  // Here you see how a single Circle instruction can be used to draw many
  // circles. To start a new circle one should use the separator ";" and
  // specify only the quantities that changed with respect to the previous
  // circle. Here we change only the center, so we need to specify the radius
  // only once!
  Circles[0.8, p1; p2; p3; p4]
]

(**expand:view:wheatstone*)(**end:expand*)
