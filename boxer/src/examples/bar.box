
include "g"
GUI = Void
Window@GUI[]


//!BOXER:REFPOINTS:BEGIN
gui1 = (8.42173175676, 2.72916714286), gui2 = (-3.56565488372, 9.39682833333)
gui3 = (53.0555553922, -4.96428689655)

//!BOXER:REFPOINTS:END
// Define a progress bar
Bar = (Real width, height, completed, Color bg, fg)

// The default parameters for the bar
([)@Bar[$$.width = 50, $$.height = 8, $$.bg = color.grey, $$.fg = color.blue]

// Bar[point] can be used to set the bar size
Point@Bar[$$.width = $.x, $$.height = $.y]

// Explain how the Window object should deal with Bar objects
Bar@@Window[
  lx = $.width, ly = $.height
  px = lx*Min[1, Max[0, $.completed]]
  \ .Poly[(0, 0), (lx, 0), (lx, ly), (0, ly), $.bg]
  \ .Poly[Gradient[.Line[(0, 0), (0, ly)], $.fg, color.white, $.fg]
          (0, 0), (px, 0), (px, ly), (0, ly)]
  \ .Poly[Style[.Fill["void"], .Border[color.black, 0.5]]
          (0, 0), (lx, 0), (lx, ly), (0, ly), .Close[]]
]

// Example using the Bar object
w = Window[][
  BBox[gui2, gui3]

  Bar[(50, 5), .completed = Point[gui1].x/50]
]

GUI[w]
