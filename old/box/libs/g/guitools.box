#provide "guitools"

///Intro: tools that may be useful while using Box GUI, Boxer.
///Section: Main.Gui Tools

include "g"

Font@Font[$$ = $]

///Intro: shows the hot points of a given {type(Window)} object.
// Every hot point is marked with a red circle. The name of the hot point is
// also written near to the point. The size of the circle and the position
// of the text are determined automatically, if not given explicitly.
// This object is typically used in documentation to create previews of a
// documented object.
///Example: ``GuiShowHotPoints[window]``
GuiShowHotPoints = ^(Real radius, Window window, Int has_font, Font font)

(.[)@GuiShowHotPoints[.radius = -1.0, .has_font = 0]

///Intro: the window whose hot points should be shown.
Window@GuiShowHotPoints[.window = $]

GuiShowHotPoints@Window[
  bbox = BBox[$.window]
  center = Point[Center[bbox]]
  radius = Real[If[$.radius >= 0.0], $.radius
                 Else, 0.05*Sqrt[Area[bbox]]]
  
  font = Font[If[$.has_font], $.font
               Else, radius*6, color.red, "sans"]

  n = Num[$.window]
  i = 0
  [If[i < n]
     pos = $.window.Get[i]
     Circle[radius, pos, color.red]

     name = \
       Str[If[$.window.HasNamedHotPoint[i]], $.window.GetHotPointName[i]
            Else, i]

     dr = pos - center
     sdr = Point[.x=Sign[dr.x], .y=Sign[dr.y]]
     displacement = Point[.x=0.5*(1 - sdr.x), .y=0.5*(1 - sdr.y)]
     
     Text[font, pos + 1.5*radius*sdr, name, From[displacement]]

   i += 1
   For[1]]
]
