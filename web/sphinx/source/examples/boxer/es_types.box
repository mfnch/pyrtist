(**expand:boot*)
(**boxer-version:0,2,0*)
include "g"
GUI = Void

(**boxer-refpoints:
bbox1 1 -100.63775510204084 72.3090779117016 bbox2 1 151.91326530612238
-4.2216603433319335
*)
(**end:expand*)color_soft_layers = Color[0.88]
color_hard_layers = Color[0.75]
cs1 = Color[color_soft_layers]
ch1 = Color[color_hard_layers]
ch2 = Dark[ch1, 0.6], cs2 = Dark[cs1, 0.6]
ch3 = Dark[ch2, 0.6], cs3 = Dark[cs2, 0.6]

b = 0.5
l = 50

skew_angle = Deg[45]

clustered = Window[][
  face1 = Window[][
    Style[Border[0.01*b]]
    Poly[ch1, (0, 0), (1, 0), (1, 1), (0, 1)]
    Circles[cs1, (0.3, 0.2), 0.1; (0.65, 0.5), 0.08;
            (0.3, 0.75), 0.09; (0.7, 0.2), 0.11;
            (0.1, 0.7), 0.07; (0.9, 0.9), 0.08]
    Hot[(0, 0), "bottom-left"; (0, 1), "top-left";
        (1, 0), "bottom-right"; (0.95, 0.9), "soft";
        (0.8, 0.7), "hard"]
  ]

  skew_vec = 0.3*Point[.x=Cos[skew_angle], .y=Sin[skew_angle]]

  face2 = Window[][
    face = Window[][
      Style[Border[0.008*b]]
      Poly[ch2, (0, 0), (1, 0), (1, 1), (0, 1)]
      Circles[cs2, (0.3, 0.2), 0.1; (0.65, 0.5), 0.08;
              (0.3, 0.75), 0.09; (0.7, 0.2), 0.11;
              (0.1, 0.7), 0.07; (0.9, 0.9), 0.08]
    ]
    SimplePut[face, Matrix[.m12=skew_vec.x, .m22=skew_vec.y]]
    Hot["bottom-left", (0, 0)]
  ]

  face3 = Window[][
    face = Window[][
      Style[Border[0.02*b]]
      Poly[ch3, (0, 0), (1, 0), (1, 1), (0, 1)]
      Circles[cs3, (0.3, 0.2), 0.1; (0.65, 0.5), 0.08;
              (0.3, 0.75), 0.09; (0.7, 0.2), 0.11;
              (0.1, 0.7), 0.07; (0.9, 0.9), 0.08]
    ]
    SimplePut[face, Matrix[.m11=skew_vec.x, .m21=skew_vec.y]]
    Hot["bottom-left", (0, 0)]
  ]

  (front = Put[face1])
  Put[face2, "t", Near["bottom-left", front.Get["top-left"]]]
  Put[face3, "t", Near["bottom-left", front.Get["bottom-right"]]]
  Hot["soft", front.Get["soft"]; "hard", front.Get["hard"]]
]

layered = Window[][
  num_layers = 5
  layer_ratio = 2.0

  hw = 1.0/(num_layers + layer_ratio*(num_layers-1))
  sw = hw*layer_ratio
  dw = hw + sw

  face1 = Window[][
    Style[Border[0.01*b]]
    Poly[cs1, (0, 0), (1, 0), (1, 1), (0, 1)]
    i = 0
    Poly[ch1, y = dw*i, (0, y), (1,  y), (1, y+hw), (0, y+hw)
         For[++i < num_layers]]
    Hot["bottom-left", (0, 0); "top-left", (0, 1);
        "bottom-right", (1, 0); "soft", (0.05, 0.85);
        "hard", (0.05, 0.73)]
  ]

  skew_vec = 0.3*Point[.x=Cos[skew_angle], .y=Sin[skew_angle]]

  face2 = Window[][
    face = Window[][
      Style[Border[0.008*b]]
      Poly[ch2, (0, 0), (1, 0), (1, 1), (0, 1)]
    ]
    SimplePut[face, Matrix[.m12=skew_vec.x, .m22=skew_vec.y]]
    Hot["bottom-left", (0, 0)]
  ]

  face3 = Window[][
    face = Window[][
      Style[Border[0.02*b]]
      Poly[cs3, (0, 0), (1, 0), (1, 1), (0, 1)]
      i = 0
      [Poly[ch3, y = dw*i, (0, y), (1,  y), (1, y+hw), (0, y+hw)]
       For[++i < num_layers]]
    ]
    SimplePut[face, Matrix[.m11=skew_vec.x, .m21=skew_vec.y]]
    Hot["bottom-left", (0, 0)]
  ]

  (front = Put[face1])
  Put[face2, "t", Near["bottom-left", front.Get["top-left"]]]
  Put[face3, "t", Near["bottom-left", front.Get["bottom-right"]]]
  Hot["soft", front.Get["soft"]; "hard", front.Get["hard"]]
]

out = Window[][
  (**expand:define-all*)
bbox1 = Point[.x=-100.637755102, .y=72.3090779117]
bbox2 = Point[.x=151.913265306, .y=-4.22166034333]

(**end:expand*)

  d = 50, x0 = -d - 0.5*l, x1 = x0 + 2*d, x2 = x1 + 2*d, y = 65
  (left = Put[clustered, (x0, 0), Scale[l]])
  (right = Put[layered, (x1, 0), Scale[l]])
  b = 5, c1 = (x2, 0.5*y), c2 = c1 + (0, 2*b)
  s = Style[Border[color.black, 0.2]]
  Rectangle[c1, c1 - (b, b), cs1, s]
  Rectangle[c2, c2 - (b, b), ch1, s]
  Texts[Font["Sans", 8], color.black
        From[(0, 0.5)], (x0, y), "a)"; (x1, y), "b)";
        c1 + b*(0.8, -0.5), From[(0, 0.5)], "soft";
        c2 + b*(0.8, -0.5), From[(0, 0.5)], "hard"]
  BBox[bbox1, bbox2]
]

out.Save["es_types.png", Window["rgb24", .Res[Dpi[50]]]]

(**expand:view:out*)(**end:expand*)
